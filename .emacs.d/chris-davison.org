#+TITLE: Chris Davison's Emacs Config
#+PROPERTY: header-args emacs-lisp :tangle yes :results silent

* Configuration
** How this works...
You /tangle/ this file, using org-babel ~C-c C-v t~, which will create ~chris-davison.el~.  This file can either be loaded from an ~init.el~ (using ~(load "chris-davison.el" t)~), or by directly symlinking ~chris-davison.el~ to ~init.el~.  The symlinking may be a bit iffy on platforms like windows...

#+BEGIN_SRC emacs-lisp :tangle no
(load "chris-davison.el" t)
#+END_SRC
** Startup
#+BEGIN_SRC emacs-lisp
;; This sets up the load path so that we can override it
(package-initialize)
(setq use-package-always-ensure t)
(setq package-enable-at-startup nil)
(add-to-list 'package-archives '("melpa" . "http://melpa.org/packages/"))
(add-to-list 'package-archives '("org" . "http://orgmode.org/elpa/") t)
#+END_SRC

We also use the ~use-package~ library to make adding new packages, and configuring them, much easier.

#+BEGIN_SRC emacs-lisp
(unless (package-installed-p 'use-package)
  (package-refresh-contents)
  (package-install 'use-package))
(require 'use-package)
#+END_SRC

** Personal Information
#+BEGIN_SRC emacs-lisp
(setq
 user-full-name "Chris Davison"
 user-mail-address "c.jr.davison@gmail.com"
 user-emacs-directory "~/code/dotfiles/.emacs.d")
#+END_SRC
** Plugins ignored
These are plugins that I used to use, and may go back to, but didn't really get used in my workflow.

- darkroom (zen-ish mode)
- ace-jump (just used isearch)
- smartparens (not doing that much lisp for the extra mental overhead)
* General init
Define a few constants to make it easy to modify behaviour by-system.
#+BEGIN_SRC emacs-lisp
(setq package-enable-at-startup nil)
(package-initialize)

;; Make list-packages take up the entire window
(use-package fullframe :ensure t :defer t
  :config (fullframe list-packages quit-window))

;; Generally useful common-lisp library (functions)
(use-package cl-lib :ensure t :config (require 'cl-lib))
(use-package s :ensure t :defer t)  ; Useful string functions
(use-package f :ensure t :defer t)  ; Useful file and dir functions

;; Make messages of some minor modes shorter
(use-package diminish :ensure t)

;; Make /apropos/ bring up everything, rather than requiring separate keybinds
(setq apropos-do-all t)

(setq-default initial-scratch-message ";; Scratch pad\n\n")

(defalias 'yes-or-no-p 'y-or-n-p)
#+END_SRC

** Delete the current file
#+BEGIN_SRC emacs-lisp
(defun delete-this-file ()
  "Delete the current file, and kill the buffer."
  (interactive)
  (or (buffer-file-name) (error "No file is currently being edited"))
  (when (yes-or-no-p (format "Really delete '%s'?"
                             (file-name-nondirectory buffer-file-name)))
    (delete-file (buffer-file-name))
    (kill-this-buffer)))
#+END_SRC
** Echo path of the current file
#+BEGIN_SRC emacs-lisp
(defun filepath ()
  "Echo the filepath as a message."
  (interactive)
  (message (buffer-file-name)))
#+END_SRC
** Try to make everything UTF8
From [[http://www.wisdomandwonder.com/wordpress/wp-content/uploads/2014/03/C3F.html][here]]
#+BEGIN_SRC emacs-lisp
(setq locale-coding-system 'utf-8)
(set-terminal-coding-system 'utf-8)
(set-keyboard-coding-system 'utf-8)
(set-selection-coding-system 'utf-8)
(prefer-coding-system 'utf-8)
(when (display-graphic-p)
  (setq x-select-request-type '(UTF8_STRING COMPOUND_TEXT TEXT STRING)))
#+END_SRC
** Show help popup if you stop halfway through a keybind sequence
#+BEGIN_SRC emacs-lisp
(use-package guide-key :ensure t :defer t
  :diminish guide-key-mode
  :init (setq guide-key/guide-key-sequence '("C-x r" "C-x 4" "C-c"))
  :config (guide-key-mode 1))
#+END_SRC
* History, Sessions, and Undo
** General history/session config
#+BEGIN_SRC emacs-lisp
(setq backup-directory-alist '(("." . "~/.emacs.d/backups")))
(setq delete-old-versions -1)
(setq version-control t)
(setq vc-make-backup-files t)
(setq auto-save-file-name-transforms
      '((".*" "~/.emacs.d/auto-save-list/" t)))

;; Save minibuffer and other history
(setq savehist-file (expand-file-name "savehist" user-emacs-directory))
(savehist-mode 1)
(setq history-length t)
(setq history-delete-duplicates t)
(setq savehist-save-minibuffer-history 1)
(setq savehist-additional-variables
      '(kill-ring
        search-ring
        regexp-search-ring))

;; Counting 'recent files' as part of history
(use-package recentf :ensure t :defer t
  :config
  (setq recentf-max-saved-items 200
        recentf-max-menu-items 15)
  (recentf-mode))


;; Always save the desktop, the current workspace config
(setq desktop-path (list user-emacs-directory)
      desktop-auto-save-timeout 600
      desktop-restore-frames nil) ; desktop-store-frames will keep themes loaded
(desktop-save-mode 1)

;; Reload when file changed on disk
(global-auto-revert-mode t)

;; Don't create lockfiles.  They're often a hassle
(setq create-lockfiles nil)
#+END_SRC
** Undo Tree - Visualise branches of undos
People often struggle with the Emacs undo model, where there's
really no concept of "redo" - you simply undo the undo.

This lets you use =C-x u= (=undo-tree-visualize=) to visually walk
through the changes you've made, undo back to a certain point (or
redo), and go down different branches.

#+BEGIN_SRC emacs-lisp
(use-package undo-tree :ensure t :defer t
  :diminish undo-tree-mode
  :config
  (global-undo-tree-mode)
  (setq undo-tree-visualizer-timestamps nil
        undo-tree-visualizer-diff t))
#+END_SRC
** Magit - Git in Emacs
#+BEGIN_SRC emacs-lisp
(use-package magit :ensure t :defer t
  :config
  (setq
   ;; Magit needs to call git multiple times
   ;; only refreshing the main buffer can improve performance
   magit-refresh-status-buffer nil
   ;; Emacs has its own version control.  We don't need to run both
   ;; as that'll be detrimental for performance
   vc-handled-backends (delq 'Git vc-handled-backends))
  :bind (("C-x g" . magit-status)))
#+END_SRC
* Editing
** General Editing Config
#+BEGIN_SRC emacs-lisp
(show-paren-mode 1)

;; transparently open compressed files
(auto-compression-mode t)

;; Don't double-space sentences
(set-default 'sentence-end-double-space nil)

;; Use lettercase to determine words in camelcase text
(global-subword-mode 1)

;; expand region
(use-package expand-region :ensure t)
(define-key global-map (kbd "C-=") 'er/expand-region)

;; If a region is selected, any 'insert' command (typing or yanking) will overwrite it, rather than inserting before.
(delete-selection-mode 1)

;; indent after newline
(global-set-key (kbd "RET") 'newline-and-indent)

;; Interactive editing...over word or region, C-; to edit.  Just modify the symbol, then repeat
;; C-; to exit.
(use-package iedit :ensure t :defer t)

;; snippets
(when (fboundp 'yas-global-mode) 
  (yas-global-mode +1))
#+END_SRC
** Text filling (paragraph reflowing)
#+BEGIN_SRC emacs-lisp
(defun unfill-paragraph (&optional region)
  "Takes a multi-line paragraph and make it into a single line of text."
  (interactive (progn (barf-if-buffer-read-only) '(t)))
  (let ((fill-column (point-max))
        ;; This would override `fill-column' if it's an integer.
        (emacs-lisp-docstring-fill-column t))
    (fill-paragraph nil region)))
(define-key global-map "\M-Q" 'unfill-paragraph)
#+END_SRC
** Managing indentation
#+BEGIN_SRC emacs-lisp
;; aggressive indentation
(use-package aggressive-indent :ensure t :defer t
  :config (global-aggressive-indent-mode))

(define-key global-map (kbd "C->") 'indent-rigidly-right-to-tab-stop)
(define-key global-map (kbd "C-<") 'indent-rigidly-left-to-tab-stop)
#+END_SRC
** Offer to create parent dirs if they do not exist
[[http://iqbalansari.github.io/blog/2014/12/07/automatically-create-parent-directories-on-visiting-a-new-file-in-emacs/][From this blog]]
#+BEGIN_SRC emacs-lisp

(defun my-create-non-existent-directory ()
  (let ((parent-directory (file-name-directory buffer-file-name)))
    (when (and (not (file-exists-p parent-directory))
               (y-or-n-p (format "Directory `%s' does not exist! Create it?" parent-directory)))
      (make-directory parent-directory t))))

(add-to-list 'find-file-not-found-functions 'my-create-non-existent-directory)
#+END_SRC
** Header templates per filetype
Use ~auto-insert~ when in a new file of that kind to insert header.
#+BEGIN_SRC emacs-lisp
(setq auto-insert-alist
      '(((emacs-lisp-mode . "Emacs lisp mode") nil
         ";;; " (file-name-nondirectory buffer-file-name) " --- " _ "\n\n"
         ";;; Commentary:\n\n"
         ";;; Code:\n\n"
         "(provide '" (substring (file-name-nondirectory buffer-file-name) 0 -3) ")\n"
         ";;; " (file-name-nondirectory buffer-file-name) " ends here\n")
        ((c-mode . "C program") nil
         "/*\n"
         " * File: " (file-name-nondirectory buffer-file-name) "\n"
         " * Description: " _ "\n"
         " */\n\n")
        ((shell-mode . "Shell script") nil
         "#!/bin/bash\n\n"
         " # File: " (file-name-nondirectory buffer-file-name) "\n"
         " # Description: " _ "\n\n")
        ((org-mode . "Org mode") nil
        "#+TITLE: " (read-string "Title: ") "\n"
        "#+AUTHOR: Chris Davison\n"
        "#+EMAIL: c.jr.davison@gmail.com\n"
        "#+OPTIONS: toc:2 num:nil html-postamble:nil\n"
        "#+PROPERTY: header-args :tangle " (read-string "Tangle filename: ") "\n")
        ((python-mode . "Python") nil
        "#!/usr/bin/env python3")
      ))
(setq auto-insert t)
(add-hook 'find-file-hook 'auto-insert)
#+END_SRC
* Languages
** Terminal Improvements
*** Windows Shell Config
Use git-bash as windows shell
#+BEGIN_SRC emacs-lisp
(defun cd/set-windows-shell ()
  "If on windows, set the shell to git bash."
  (interactive)
  (if (eq system-type 'windows-nt)
      (progn (setq explicit-shell-file-name
                   "C:/Program Files/Git/bin/sh.exe")
             (setq shell-file-name "bash")
             (setq explicit-sh.exe-args '("--login" "-i"))
             (setenv "SHELL" shell-file-name)
             (add-hook 'comint-output-filter-functions 'comint-strip-ctrl-m))
    nil))

(cd/set-windows-shell)
#+END_SRC
*** Improvements for ansi-term
#+BEGIN_SRC emacs-lisp
(defadvice term-sentinel (around my-advice-term-sentinel (proc msg))
  "Close an ansi-term buffer if I quit the terminal."
  (if (memq (process-status proc) '(signal exit))
      (let ((buffer (process-buffer proc)))
        ad-do-it
        (kill-buffer buffer))
    ad-do-it))
(ad-activate 'term-sentinel)

;; By default, use fish in ansi-term
;; e.g. don't prompt for a shell
(defvar my-term-shell "/usr/local/bin/fish")
(defadvice ansi-term (before force-bash)
  (interactive (list my-term-shell)))
(ad-activate 'ansi-term)

;; Use UTF8 in terminals
(defun my-term-use-utf8 ()
  (set-buffer-process-coding-system 'utf-8-unix 'utf-8-unix))
(add-hook 'term-exec-hook 'my-term-use-utf8)

;; Make URLs in the term clickable
(defun my-term-paste (&optional string)
  (interactive)
  (process-send-string
   (get-buffer-process (current-buffer))
   (if string string (current-kill 0))))

(defun my-term-hook ()
  (goto-address-mode)
  (define-key term-raw-map "\C-y" 'my-term-paste))
(add-hook 'term-mode-hook 'my-term-hook)
#+END_SRC
** Markdown
#+BEGIN_SRC emacs-lisp
(use-package markdown-mode :ensure t :defer t
  :config
  (add-to-list 'auto-mode-alist
               (cons "\\.\\(md\\|markdown\\)\\'" 'markdown-mode))
  (add-hook 'markdown-mode-hook 'visual-line-mode))
#+END_SRC

Use a /let/ binding so that I can Ctrl-g to cancel without leaving characters laying around
#+BEGIN_SRC emacs-lisp
(defun cd/yank-md ()
  "Yank a markdown link and enter a description for it."
  (interactive)
  (let ((desc (read-string "Description: ")))
    (insert "[")
    (insert desc)
    (insert "](")
    (yank)
    (insert ")")))
(global-set-key (kbd "<f5>") 'cd/yank-md)
#+END_SRC
** Company -- COMPlete ANYthing
#+BEGIN_SRC emacs-lisp
(use-package company :ensure t :diminish company-mode :defer t
  :bind ("TAB" . company-indent-or-complete-common)
  :config
  (setq company-tooltip-align-annotations t
        company-global-modes '(not org-mode))
  (add-hook 'prog-mode-hook 'company-mode))
#+END_SRC
** CSV
#+BEGIN_SRC emacs-lisp
(use-package csv-mode :defer t
  :mode ("\\.[Cc][Ss][Vv]\\'" . csv-mode)
  :config (setq csv-separators '("," ";" "|" " ")))
#+END_SRC
** Flycheck
#+BEGIN_SRC emacs-lisp
(use-package flycheck :ensure t :defer t
  :config
  (setq-default 
   flycheck-disabled-checkers
   (append flycheck-disabled-checkers '(javascript-jshint) '(json-jsonlist))
   flycheck-temp-prefix ".flycheck")
  (flycheck-add-mode 'javascript-eslint 'web-mode)
  (add-hook 'after-init-hook #'global-flycheck-mode))
#+END_SRC
** Lisps -- Clojure and Cider
#+BEGIN_SRC emacs-lisp
;; Cider for interactive clojure programming
(use-package flycheck-clojure :ensure t :defer t)
(use-package cider :ensure t :defer t
  :config
  (setq nrepl-popup-stacktraces nil)
  (with-eval-after-load 'cider
    (add-hook 'cider-mode-hook 'eldoc-mode)
    (add-hook 'cider-repl-mode-hook 'subword-mode)
    (add-hook 'cider-repl-mode-hook 'smartparens-mode)
    (add-hook 'cider-repl-mode-hook 'paredit-mode)
    (with-eval-after-load 'clojure-mode
      (with-eval-after-load 'flycheck
        (flycheck-clojure-setup)))))
#+END_SRC
** Golang
#+BEGIN_SRC emacs-lisp
(use-package go-mode :ensure t :defer t
  :config
  (add-hook 'before-save-hook 'gofmt-before-save)
  (setq gofmt-command "goimports"))
#+END_SRC
** Rust / Racer config
Auto-completion for rust, using racer
#+BEGIN_SRC emacs-lisp
(use-package rust-mode :ensure t :defer t)
(use-package flymake-rust :ensure t :defer t)
(use-package flycheck-rust :ensure t :defer t)
(use-package cargo :ensure t :defer t)
(use-package racer :defer t
  :ensure t
  :config
  (setq racer-cmd "/Users/davison/prog/z__NOT_MINE/racer/target/release/racer")
  (setq racer-rust-src-path "/Users/davison/prog/z__NOT_MINE/rust_1.3_src/src/")
  (add-hook 'rust-mode-hook 'racer-mode)
  (add-hook 'racer-mode-hook 'eldoc-mode)
  (add-hook 'rust-mode-hook 'cargo-minor-mode)
  (add-hook 'racer-mode-hook 'company-mode))
#+END_SRC
** Python configuration
Use elpy for python
#+BEGIN_SRC emacs-lisp
;; (use-package ob-ipython :ensure t)
(use-package elpy :ensure t :defer t
  :config
  (add-hook 'python-mode-hook (lambda () (elpy-enable)))
  (setq python-shell-interpreter "ipython"
        python-shell-interpreter-args "--simple-prompt -i")
  (diminish 'elpy-mode))

(defun ipython()
  (interactive)
  (if (eq system-type 'windows-nt)
      (progn (setq explicit-shell-file-name
                   "C:/python3/scripts/ipython.exe")
             (setq shell-file-name "ipython")
             (setq explicit-sh.exe-args '("--login" "-i"))
             (setenv "SHELL" shell-file-name)
             (add-hook 'comint-output-filter-functions 'comint-strip-ctrl-m)
             (shell)
             (cd/set-windows-shell))
    (ansi-term "~/.envs/ml/bin/ipython" "ipython")))
#+END_SRC

** WIP Latex config
#+BEGIN_SRC emacs-lisp
(add-hook 'LaTeX-mode-hook 'visual-line-mode)
#+END_SRC

#+BEGIN_SRC emacs-lisp :tangle no
(use-package auctex :ensure t :defer t
  :config
  (setq TeX-auto-save t
        TeX-parse-self t
        TeX-save-query nil
        ispell-program-name "aspell"
        ispell-dictionary "english")
  (add-hook 'LaTeX-mode-hook 'flyspell-mode)
  (add-hook 'LaTeX-mode-hook 'flyspell-buffer))

(defun turn-on-outline-minor-mode ()
  (outline-minor-mode 1))

(add-hook 'LaTeX-mode-hook 'turn-on-outline-minor-mode)
(add-hook 'latex-mode-hook 'visual-line-mode)

(setq outline-minor-mode-prefix "\C-c \C-o") ; Or something else

;; Manage citations
(require 'tex-site)
(autoload 'reftex-mode "reftex" "RefTeX Minor Mode" t)
(autoload 'turn-on-reftex "reftex" "RefTeX Minor Mode" nil)
(autoload 'reftex-citation "reftex-cite" "Make citation" nil)
(autoload 'reftex-index-phrase-mode "reftex-index" "Phrase Mode" t)
(add-hook 'latex-mode-hook 'turn-on-reftex)
(add-hook 'LaTeX-mode-hook 'turn-on-reftex)

(setq LaTeX-eqnarray-label "eq"
      LaTeX-equation-label "eq"
      LaTeX-figure-label "fig"
      LaTeX-table-label "tab"
      LaTeX-myChapter-label "chap"
      TeX-auto-save t
      TeX-newline-function 'reindent-then-newline-and-indent
      TeX-parse-self t
      TeX-style-path
      '("style/" "auto/"
        "/usr/share/emacs21/site-lisp/auctex/style/"
        "/var/lib/auctex/emacs21/"
        "/usr/local/share/emacs/site-lisp/auctex/style/")
      LaTeX-section-hook
      '(LaTeX-section-heading
        LaTeX-section-title
        LaTeX-section-toc
        LaTeX-section-section
        LaTeX-section-label))
#+END_SRC
** Julia
#+BEGIN_SRC emacs-lisp
(use-package julia-mode :ensure t :defer t)
(use-package julia-repl :ensure t :defer t
  :config
  (add-hook 'julia-mode-hook 'julia-repl-mode))

(add-to-list 'load-path "C:/Julia-1.1.0/bin")
(if (eq system-type 'windows-nt)
    (setq julia-repl-executable-records '((default "julia.exe" :basedir "C:/Julia-1.1.0/bin" ))))

;; (setq julia-repl-executable-records '((default "julia")))
#+END_SRC
** Web-stuff
#+BEGIN_SRC emacs-lisp
(use-package sass-mode :ensure t :defer t)
(use-package js2-mode :ensure t :defer t)

;; Colourize CSS literals
(use-package rainbow-mode :ensure t :defer t
  :config 
  (add-hook 'css-mode-hook 'rainbow-mode)
  (add-hook 'html-mode-hook 'rainbow-mode)
  (add-hook 'sass-mode-hook 'rainbow-mode))
#+END_SRC
* Navigation
** General Navigation config
Navigation of prtty much any /menu-type/ thing
#+BEGIN_SRC emacs-lisp

(use-package ivy :ensure t :defer t
  :diminish (ivy-mode . "")
  :bind
  (:map ivy-mode-map
        ("C-c h" . ivy-switch-buffer)
        ("C-c s" . swiper))
  :config
  (ivy-mode 1)
  (setq ivy-use-virtual-buffers t
        ivy-height 15
        ivy-count-format ""
        ivy-initial-inputs-alist nil
        ivy-re-builders-alist
        '((t . ivy--regex-plus))))
(use-package counsel :ensure t :defer t
  :bind*
  (("C-x f" . counsel-find-file)
   ("C-c i" . counsel-imenu)
   ("C-c a" . counsel-rg)
   ("C-c g s" . counsel-grep-or-swiper)
   ("C-c b" . counsel-descbinds)
   ("M-x" . counsel-M-x)))

(use-package swiper :ensure t :defer t)

;; do imenu over every file with the same major mode, recursively
(use-package imenu-anywhere :ensure t :defer t)
#+END_SRC
** Navigate between windows
Give HUD prompt when changing window
#+BEGIN_SRC emacs-lisp
;; Prompt with a hud when switching windows, if more than 2 windows
(use-package switch-window :defer t
  :ensure t
  :bind ("C-x o" . switch-window))
#+END_SRC

Navigate between windows with ~shift+<direction>~
#+BEGIN_SRC emacs-lisp
(windmove-default-keybindings)
#+END_SRC

Cycle through windows (e.g. panes)
#+BEGIN_SRC emacs-lisp
;; cycle through 'windows' (e.g. panes)
(define-key global-map (kbd "M-`") 'next-multiframe-window)
(define-key global-map (kbd "C-M-`") 'previous-multiframe-window)
#+END_SRC
** ibuffer -- show version control status of file
#+BEGIN_SRC emacs-lisp
;; Interactively modify buffer list
(use-package fullframe :ensure t :defer t)
(with-eval-after-load 'buffer (fullframe ibuffer ibuffer-quit))

(use-package ibuffer-vc :ensure t)

(defun ibuffer-set-up-preferred-filters ()
  (ibuffer-vc-set-filter-groups-by-vc-root)
  (unless (eq ibuffer-sorting-mode 'filename/process)
    (ibuffer-do-sort-by-filename/process)))

(add-hook 'ibuffer-hook 'ibuffer-set-up-preferred-filters)

(with-eval-after-load 'ibuffer
  ;; Use human readable Size column instead of original one
  (define-ibuffer-column size-h
    (:name "Size" :inline t)
    (cond
     ((> (buffer-size) 1000000) (format "%7.1fM" (/ (buffer-size) 1000000.0)))
     ((> (buffer-size) 1000) (format "%7.1fk" (/ (buffer-size) 1000.0)))
     (t (format "%8d" (buffer-size)))))
  ;; Explicitly require ibuffer-vc to get its column definitions, which
  ;; can't be autoloaded
  (require 'ibuffer-vc))


;; Modify the default ibuffer-formats (toggle with `)
(setq ibuffer-formats
      '((mark modified read-only vc-status-mini " "
              (name 18 18 :left :elide) " "
              (size-h 9 -1 :right) " "
              (mode 16 16 :left :elide) " "
              (vc-status 16 16 :left) " "
              filename-and-process)
        (mark modified read-only vc-status-mini " "
              (name 18 18 :left :elide) " "
              (size-h 9 -1 :right) " "
              (mode 16 16 :left :elide) " "
              filename-and-process)
        ))
(setq ibuffer-filter-group-name-face 'font-lock-doc-face)
(global-set-key (kbd "C-x C-b") 'ibuffer)
#+END_SRC
** isearch -- live preview of search and replace
#+BEGIN_SRC emacs-lisp
;; Show current and total matches while searching
(use-package anzu :defer t
  :ensure t
  :diminish anzu-mode
  :bind (([remap query-replace-regexp] . anzu-query-replace-regexp)
         ([remap query-replace] . anzu-query-replace)
         ;; Invert regex and normal isearch
         ("C-M-%" . anzu-query-replace)
         ("M-%" . anzu-query-replace-regexp))
  :config (global-anzu-mode t))

;; DEL during isearch should edit the search string, not jump back to the previous result
(define-key isearch-mode-map [remap isearch-delete-char] 'isearch-del-char)
#+END_SRC

* OSX
#+BEGIN_SRC emacs-lisp
(when (eq system-type 'darwin)
  (use-package exec-path-from-shell :ensure t
    :config
    (when (memq window-system '(mac ns))
      (exec-path-from-shell-initialize))
    (exec-path-from-shell-copy-env "GOPATH"))
  
  (setq mac-command-modifier 'meta
        mac-option-modifier 'none
        default-input-method "MacOSX")
    
  ;;Make the mouse wheel/trackpad less jerky
  (setq mouse-wheel-scroll-amount '(1 ((shift) . 5) ((control))))
  (dolist (multiple '("" "double-" "triple-"))
    (dolist (direction '("right" "left"))
      (global-set-key (kbd (concat "<" multiple "wheel-" direction ">")) 'ignore)))

  ;;And give emacs some of the expected OS X keybinds
  (global-set-key (kbd "M-h") 'ns-do-hide-emacs)
  (global-set-key (kbd "M-˙") 'ns-do-hide-others)
  (with-eval-after-load 'nxml-mode (define-key nxml-mode-map (kbd "M-h") nil))
  (global-set-key (kbd "M-ˍ") 'ns-do-hide-others) ;; what describe-key reports for cmd-option-h
  (global-set-key (kbd "M-<up>") 'toggle-frame-fullscreen) ;;Bind Meta-<UP> to fullscreen toggling
  (global-set-key (kbd "<f10>") 'toggle-frame-fullscreen) ;;Bind Meta-<UP> to fullscreen toggling
  )
#+END_SRC
* Org-mode
Org-mode is a really powerful notetaking tool.

You can easily /capture/ information using various different templates (including custom templates), and then refile them to perhaps a more appropriate location,

/Agenda/ lets you schedule and deadline tasks.
** General Org Config
#+BEGIN_SRC emacs-lisp
(use-package org
  :ensure t
  :bind (("<f1>" . org-capture)
         ("<f2>" . org-agenda)
         ("<f3>" . org-agenda-list)
         ("C-c l" . org-store-link))
  :config
  (setq org-directory "~/Dropbox/"
        org-default-notes-file "~/Dropbox/inbox.org"
        org-src-window-setup 'current-window
        org-src-fontify-natively t
        org-src-tab-acts-natively t
        org-confirm-babel-evaluate nil
        org-edit-src-content-indentation 0
        org-catch-invisible-edits 'show-and-error
        org-imenu-depth 3
        ;; Use M-+ M-- to change todo, and leave S-<arrow> for windows
        org-replace-disputed-keys t 
        inhibit-compacting-font-caches t
        org-todo-keywords
        '(
          (sequence "TODO" "WIP" "|" "DONE")
          (sequence "|" "CANCELLED")
          (sequence "|" "BACKBURNER")
          )
        org-startup-indented t
        org-hide-leading-stars t
        org-cycle-separator-lines 0
        org-list-indent-offset 1
        org-modules '(org-bibtex org-habit)
        org-agenda-files '("~/Dropbox/" "~/Dropbox/projects" "~/Dropbox/archive")
        org-log-done 'time
        org-ellipsis "↴"
        org-archive-location "~/Dropbox/archive.org::")
  (diminish 'org-indent-mode)
  (org-babel-do-load-languages
   'org-babel-load-languages
   '(
     (python . t)
     (R . t)
     (sqlite . t)
     (perl . t)
     ))
  (setq org-confirm-babel-evaluate nil)
  (setq )
  ;; Settings for refiling
  (setq org-reverse-note-order t
        org-refile-use-outline-path nil
        org-refile-allow-creating-parent-nodes 'confirm
        org-refile-use-cache nil
        org-refile-targets '((org-agenda-files . (:maxlevel . 3)))
        org-blank-before-new-entry nil)
  ;; (add-hook 'org-mode-hook 'auto-fill-mode)
  ;; (add-hook 'org-mode-hook 'visual-fill-column-mode)
  (add-hook 'org-mode-hook 'visual-line-mode)
  (add-hook 'org-mode-hook 'org-indent-mode)
  (setq fill-column 80))


#+END_SRC
** Insert a link from clipboard
Prompt for description. Basically slightly quicker than having to ~C-y~ when creating a link.
#+BEGIN_SRC emacs-lisp
;; (use-package ox-reveal :ensure t)
(defun insert-link-with-description-prompt ()
  "Yank into an org link."
  (interactive)
  (let ((description (read-string "Description: ")))
    (insert "[[")
    (yank)
    (insert (s-concat "][" description "]]"))))
#+END_SRC
** Fix indentation for org source blocks
#+BEGIN_SRC emacs-lisp
(defun cd/org-cleanup ()
  (interactive)
  (org-edit-special)
  (indent-region (point-min) (point-max))
  (org-edit-src-exit))
(global-set-key (kbd "C-x c") 'cd/org-cleanup)
#+END_SRC
** Templates for src/latex/etc blocks
#+BEGIN_SRC emacs-lisp
(setq org-structure-template-alist
      '(("a" . "export ascii")
        ("c" . "center")
        ("C" . "comment")
        ("e" . "example")
        ("E" . "export")
        ("h" . "export html")
        ("l" . "src emacs-lisp")
        ("q" . "quote")
        ("s" . "src")
        ("v" . "verse")
        ("V" . "verbatim")))
#+END_SRC
** Move to prev/next narrow
#+BEGIN_SRC emacs-lisp
(defun cd/move-to-previous-narrow ()
  (interactive)
  (progn
    (beginning-of-buffer)
    (widen)
    (outline-previous-heading)
    (org-narrow-to-subtree)))

(defun cd/move-to-next-narrow ()
  (interactive)
  (progn
    (beginning-of-buffer)
    (widen)
    (outline-next-heading)
    (org-narrow-to-subtree)))
#+END_SRC
** Capture templates
#+BEGIN_SRC emacs-lisp
(setq org-capture-templates
      '(("q" "Quotes" entry (file+headline "~/Dropbox/reference/quotes.org" "UNFILED")
         "* %^{Topic}\n#+BEGIN_QUOTE\n%^{Quote} (%^{Author})\n#+END_QUOTE" :immediate-finish 1)

        ("u" "URL" item (file+headline "~/Dropbox/inbox.org" "Links")
         "[[%^{URL}][%^{DESCRIPTION}]] %^{COMMENTS}\n" :immediate-finish 1)

        ("p" "Project" entry (file "~/Dropbox/inbox.org") "* TODO %^{PROJECT}")

        ("a" "Article" entry (file+headline "~/Dropbox/inbox.org" "Literature")
         "* TODO %^{Title}
:PROPERTIES:
:CREATED: %U
:END:

Abstract:
%?"
         :prepend t
         :created t)

        ;; Todos (tasks within inbox)
        ;; Header-bullet of -TODO- <TASK>, under the TASKS L1 header
        ("t" "Todo")
        ("tt" "Todo" entry (file+headline "~/Dropbox/inbox.org" "Tasks")
         "* TODO %^{TASK}" :immediate-finish 1)
        ("tT" "Todo with pasted clipboard" entry (file+headline "~/Dropbox/inbox.org" "Tasks")
         "* TODO %^{TASK}\n%c" :immediate-finish 1)
        ("ts" "Todo with date" entry (file+headline "~/Dropbox/inbox.org" "Tasks")
         "* TODO %^{TASK}\n\nSCHEDULED: %^t" :immediate-finish 1)
        ("td" "Todo with deadline" entry (file+headline "~/Dropbox/inbox.org" "Tasks")
         "* TODO %^{TASK}\n\nDEADLINE: %^t" :immediate-finish 1)


        ;; Journal
        ;; Datetree of YYYY / YYYY-MM MONTHNAME / YYYY-MM-DD DAYNAME
        ("j" "Journal entry")
        ("jj" "Quick entry" item (file+datetree "~/Dropbox/journal.org")
         "%^{Journal}" :immediate-finish 1)
        ("jJ" "Full entry" item (file+datetree "~/Dropbox/journal.org")
         "%?")

        ;; LOGBOOK
        ("l" "Logbook entry")
        ("ll" "Quick entry" item (file+datetree "~/Dropbox/logbook.org")
         "%^{Logbook}" :immediate-finish 1)
        ("lL" "Full entry" item (file+datetree "~/Dropbox/logbook.org")
         "%?")

        ;; Note in Inbox
        ("n" "Note in Inbox")
        ("nn" "Note" item (file+headline "~/Dropbox/inbox.org" "Notes")
         "%^{NOTE}" :immediate-finish 1)
        ("nN" "Note with clipboard" item (file+headline "~/Dropbox/inbox.org" "Notes")
         "%^{NOTE} %c")
        ("ne" "Note" entry (file+headline "~/Dropbox/inbox.org" "Notes")
         "* %^{Title}\n%?")

        ("c" "Code Snippet" entry (file+headline "~/Dropbox/inbox.org" "Code Snippets")
         "* %^{Snippet Topic}\n#+BEGIN_SRC %^{Language}\n%c\n#+END_SRC\n")
        ))
#+END_SRC
* Hydra -- General utility HUDs
#+BEGIN_SRC emacs-lisp
(use-package hydra :ensure t :defer t)
#+END_SRC

** Dired
#+BEGIN_SRC emacs-lisp
(defhydra hydra-dired (:hint nil :color pink)
  "
_+_ mkdir          _v_iew           _m_ark             _(_ details        _i_nsert-subdir    wdired
_C_opy             _O_ view other   _U_nmark all       _)_ omit-mode      _$_ hide-subdir    C-x C-q : edit
_D_elete           _o_pen other     _u_nmark           _l_ redisplay      _w_ kill-subdir    C-c C-c : commit
_R_ename           _M_ chmod        _t_oggle           _g_ revert buf     _e_ ediff          C-c ESC : abort
_Y_ rel symlink    _G_ chgrp        _E_xtension mark   _s_ort             _=_ pdiff
_S_ymlink          ^ ^              _F_ind marked      _._ toggle hydra   \\ flyspell
_r_sync            ^ ^              _f_ilter           ^ ^                _?_ summary
_z_ compress-file  _A_ find regexp
_Z_ compress       _Q_ repl regexp

T - tag prefix
"
  ("\\" dired-do-ispell)
  ("(" dired-hide-details-mode)
  (")" dired-omit-mode)
  ("+" dired-create-directory)
  ("=" diredp-ediff)         ;; smart diff
  ("?" dired-summary)
  ("$" diredp-hide-subdir-nomove)
  ("A" dired-do-find-regexp)
  ("C" dired-do-copy)        ;; Copy all marked files
  ("D" dired-do-delete)
  ("E" dired-mark-extension)
  ("e" dired-ediff-files)
  ("f" (progn
         (dired-mark-files-regexp (read-string "Regexp: "))
         (dired-toggle-marks)
         (dired-do-kill-lines)) :exit t)
  ("F" dired-do-find-marked-files)
  ("G" dired-do-chgrp)
  ("g" revert-buffer)        ;; read all directories again (refresh)
  ("i" dired-maybe-insert-subdir)
  ("l" dired-do-redisplay)   ;; relist the marked or singel directory
  ("M" dired-do-chmod)
  ("m" dired-mark)
  ("O" dired-display-file)
  ("o" dired-find-file-other-window)
  ("Q" dired-do-find-regexp-and-replace)
  ("R" dired-do-rename)
  ("r" dired-do-rsynch)
  ("S" dired-do-symlink)
  ("s" dired-sort-toggle-or-edit)
  ("t" dired-toggle-marks)
  ("U" dired-unmark-all-marks)
  ("u" dired-unmark)
  ("v" dired-view-file)      ;; q to exit, s to search, = gets line #
  ("w" dired-kill-subdir)
  ("Y" dired-do-relsymlink)
  ("z" diredp-compress-this-file)
  ("Z" dired-do-compress)
  ("q" nil)
  ("." nil :color blue))

(with-eval-after-load 'dired
  (define-key dired-mode-map "." 'hydra-dired/body))

#+END_SRC
** Ibuffer
#+BEGIN_SRC emacs-lisp
(defhydra hydra-ibuffer-main (:color pink :hint nil)
  "
 ^Navigation^ | ^Mark^        | ^Actions^        | ^View^
-^----------^-+-^----^--------+-^-------^--------+-^----^-------
  _k_:    ʌ   | _m_: mark     | _D_: delete      | _g_: refresh
 _RET_: visit | _u_: unmark   | _S_: save        | _s_: sort
  _j_:    v   | _*_: specific | _a_: all actions | _/_: filter
-^----------^-+-^----^--------+-^-------^--------+-^----^-------
"
  ("j" ibuffer-forward-line)
  ("RET" ibuffer-visit-buffer :color blue)
  ("k" ibuffer-backward-line)

  ("m" ibuffer-mark-forward)
  ("u" ibuffer-unmark-forward)
  ("*" hydra-ibuffer-mark/body :color blue)

  ("D" ibuffer-do-delete)
  ("S" ibuffer-do-save)
  ("a" hydra-ibuffer-action/body :color blue)

  ("g" ibuffer-update)
  ("s" hydra-ibuffer-sort/body :color blue)
  ("/" hydra-ibuffer-filter/body :color blue)

  ("o" ibuffer-visit-buffer-other-window "other window" :color blue)
  ("q" quit-window "quit ibuffer" :color blue)
  ("." nil "toggle hydra" :color blue))

(defhydra hydra-ibuffer-mark (:color teal :columns 5
                              :after-exit (hydra-ibuffer-main/body))
  "Mark"
  ("*" ibuffer-unmark-all "unmark all")
  ("M" ibuffer-mark-by-mode "mode")
  ("m" ibuffer-mark-modified-buffers "modified")
  ("u" ibuffer-mark-unsaved-buffers "unsaved")
  ("s" ibuffer-mark-special-buffers "special")
  ("r" ibuffer-mark-read-only-buffers "read-only")
  ("/" ibuffer-mark-dired-buffers "dired")
  ("e" ibuffer-mark-dissociated-buffers "dissociated")
  ("h" ibuffer-mark-help-buffers "help")
  ("z" ibuffer-mark-compressed-file-buffers "compressed")
  ("b" hydra-ibuffer-main/body "back" :color blue))

(defhydra hydra-ibuffer-action (:color teal :columns 4
                                :after-exit
                                (if (eq major-mode 'ibuffer-mode)
                                    (hydra-ibuffer-main/body)))
  "Action"
  ("A" ibuffer-do-view "view")
  ("E" ibuffer-do-eval "eval")
  ("F" ibuffer-do-shell-command-file "shell-command-file")
  ("I" ibuffer-do-query-replace-regexp "query-replace-regexp")
  ("H" ibuffer-do-view-other-frame "view-other-frame")
  ("N" ibuffer-do-shell-command-pipe-replace "shell-cmd-pipe-replace")
  ("M" ibuffer-do-toggle-modified "toggle-modified")
  ("O" ibuffer-do-occur "occur")
  ("P" ibuffer-do-print "print")
  ("Q" ibuffer-do-query-replace "query-replace")
  ("R" ibuffer-do-rename-uniquely "rename-uniquely")
  ("T" ibuffer-do-toggle-read-only "toggle-read-only")
  ("U" ibuffer-do-replace-regexp "replace-regexp")
  ("V" ibuffer-do-revert "revert")
  ("W" ibuffer-do-view-and-eval "view-and-eval")
  ("X" ibuffer-do-shell-command-pipe "shell-command-pipe")
  ("b" nil "back"))

(defhydra hydra-ibuffer-sort (:color amaranth :columns 3)
  "Sort"
  ("i" ibuffer-invert-sorting "invert")
  ("a" ibuffer-do-sort-by-alphabetic "alphabetic")
  ("v" ibuffer-do-sort-by-recency "recently used")
  ("s" ibuffer-do-sort-by-size "size")
  ("f" ibuffer-do-sort-by-filename/process "filename")
  ("m" ibuffer-do-sort-by-major-mode "mode")
  ("b" hydra-ibuffer-main/body "back" :color blue))

(defhydra hydra-ibuffer-filter (:color amaranth :columns 4)
  "Filter"
  ("m" ibuffer-filter-by-used-mode "mode")
  ("M" ibuffer-filter-by-derived-mode "derived mode")
  ("n" ibuffer-filter-by-name "name")
  ("c" ibuffer-filter-by-content "content")
  ("e" ibuffer-filter-by-predicate "predicate")
  ("f" ibuffer-filter-by-filename "filename")
  (">" ibuffer-filter-by-size-gt "size")
  ("<" ibuffer-filter-by-size-lt "size")
  ("/" ibuffer-filter-disable "disable")
  ("b" hydra-ibuffer-main/body "back" :color blue))

(define-key ibuffer-mode-map "." 'hydra-ibuffer-main/body)

(add-hook 'ibuffer-hook 'hydra-ibuffer-main/body)
#+END_SRC
** My utility binds
#+BEGIN_SRC emacs-lisp
(defhydra cd/zoom (:exit nil)
  "Text scale"
  ("+" text-scale-increase "Zoom in" :column "Zoom")
  ("-" text-scale-decrease "Zoom out" :column "Zoom")
  ("." nil "quit"))

(defhydra cd/indent (:exit nil)
  (">" indent-rigidly-right-to-tab-stop "Indent to tabstop")
  ("<" indent-rigidly-left-to-tab-stop "Dedent to tabstop")
  ("<right>" indent-rigidly-right-to-tab-stop "Indent to tabstop")
  ("<left>" indent-rigidly-left-to-tab-stop "Dedent to tabstop")
  ("." nil))

(defhydra cd/paragraphs-and-lines (:exit t)
  ("<up>" delete-indentation "Join line UP" :exit nil)
  ("<down>" (join-line -1) "Join line DOWN" :exit nil)
  ("u" unfill-paragraph "Unfill paragraph")
  ("f" fill-paragraph "Fill paragraph"))

(defhydra cd/links (:exit t)
  ("c" insert-link-with-description-prompt "From clipboard" :column "Insert")
  ("l" org-insert-link "Prompted" :column "Insert")
  ("s" org-store-link "Store" :column "Store"))

(defhydra cd/windowmove (:exit t)
  ("<left>" windmove-left "Left" :column "Move")
  ("<right>" windmove-right "Right" :column "Move")
  ("<up>" windmove-up "Up" :column "Move")
  ("<down>" windmove-down "Down" :column "Move")
  ("R" (progn (split-window-right) (windmove-right)) "Split right" :column "Move")
  ("D" (progn (split-window-below) (windmove-down)) "Split down" :column "Move")
  ("n" cd/move-to-next-narrow "Next narrowed subtree" :column "Move" :exit nil)
  ("p" cd/move-to-previous-narrow "Previous narrowed subtree" :column "Move" :exit nil)

  ("S-<up>" enlarge-window "Enlarge" :column "Windows" :exit nil)
  ("S-<down>" shrink-window "Shrink" :column "Windows" :exit nil)
  ("=" balance-windows "Equalise" :column "Windows")
    ("d" delete-window "Close window" :column "Windows")
  ("o" delete-other-windows "Keep only this window" :column "Windows"))

(defhydra cd/files (:exit t)
  ("i" (find-file "~/Dropbox/inbox.org") "Inbox")
  ("j" (find-file "~/Dropbox/journal.org") "Journal")
  ("l" (find-file "~/Dropbox/logbook.org") "Logbook")
  ("a" (find-file "~/Dropbox/archive.org") "Archive")
  ("f" counsel-find-file "Find file")
  ("F" counsel-file-jump "Find file (recursive)")
  ("b" ivy-switch-buffer "Switch Buffer")
  ("c" (find-file "~/code/dotfiles/.emacs.d/chris-davison.org") "Config")
  ("d" dired "Dired"))

(defhydra hydra-fold (:pre (hs-minor-mode 1))
  "fold"
  ("t" fold-dwim-toggle "toggle")
  ("h" fold-dwim-hide-all "hide-all")
  ("s" fold-dwim-show-all "show-all")
  ("q" nil "quit"))

(defhydra hydra-cd-util (:hint nil :exit t)
  ("<tab>" cd/indent/body "Indent" :column "Appearance")
  ("f" hydra-fold/body "Fold" :column "Appearance")
  ("z" cd/zoom/body "Font size" :column "Appearance")
  ("C-z" (modi/toggle-one-window nil t) "Zen mode" :column "Appearance")
  ("w" cd/windowmove/body "Windows" :column "Appearance")

  ("s" counsel-grep-or-swiper "Swiper" :column "Search & Replace")
  ("r" anzu-query-replace-regexp "Replace" :column "Search & Replace")
  ("i" ivy-imenu-anywhere "Imenu Anywhere" :column "Search & Replace")
  ("e" iedit-mode "Iedit" :column "Search & Replace")

  ("c" (progn (mark-whole-buffer) (kill-ring-save 0 0 t)) "Copy buffer" :column "Utils")
  ("l" cd/links/body "Links" :column "Utils")
  ("o" cd/files/body "Open file bookmarks" :column "Utils")
  ("a" apropos "Apropos" :column "Utils")
  ("p" cd/paragraphs-and-lines/body "Paragraphs and lines" :column "Utils"))

(define-key global-map (kbd "M-c") 'hydra-cd-util/body)
(define-key global-map (kbd "C-z") 'hydra-cd-util/body)
#+END_SRC
* Appearance
** Font
List some pretty fonts, and grab the first one that's available.
#+BEGIN_SRC emacs-lisp
(setq cd-font-options 
      '("Fantasque Sans Mono" 
        "Ubuntu Mono" 
        "Roboto Mono"
        "DejaVu Sans Mono" 
        "Fira Mono"
        "Liberation Mono"))
(setq cd-fonts-available
      (cl-remove-if
       (lambda (font)
         (not (member font (font-family-list)))) cd-font-options))
(when cd-fonts-available
  (set-frame-font (car cd-fonts-available) 1))

(setq line-spacing 0.1)
(setq cd-font-height
      (if (eq system-type 'windows-nt) 160 200))
(set-face-attribute 'default nil :height cd-font-height)
#+END_SRC
** Colour themes
Disable themes before loading a new theme
#+BEGIN_SRC emacs-lisp
(defadvice load-theme (before theme-dont-propagate activate)
  (mapc #'disable-theme custom-enabled-themes))
(setq custom-safe-themes t)
#+END_SRC

#+BEGIN_SRC emacs-lisp
(use-package kaolin-themes)
(load-theme 'kaolin-dark t)
#+END_SRC
** Suppress GUI features
#+BEGIN_SRC emacs-lisp
(setq use-file-dialog nil
      use-dialog-box nil
      menu-bar-mode nil
      inhibit-startup-screen t
      inhibit-startup-echo-area-message t)

;; Hide tool bar,  scroll bar and borders
(when (fboundp 'tool-bar-mode) (tool-bar-mode -1))
(when (fboundp 'set-scroll-bar-mode) (set-scroll-bar-mode nil))

(let ((no-border '(internal-border-width . 0)))
  (add-to-list 'default-frame-alist no-border)
  (add-to-list 'initial-frame-alist no-border))
#+END_SRC

** Line highlighting, linum, colnum, and cursor
#+BEGIN_SRC emacs-lisp
(global-hl-line-mode 1)
(blink-cursor-mode 0)
(setq linum-format "%d ")

;; Line number and column 
(line-number-mode 1)
(column-number-mode 1)
#+END_SRC
** Tabstop stuff
#+BEGIN_SRC emacs-lisp
(setq tab-stop-list (number-sequence 4 200 4))
(setq-default indent-tabs-mode nil)
(setq-default tab-width 4)
#+END_SRC
** Don't scroll jump multiple lines
#+BEGIN_SRC emacs-lisp
(setq scroll-step           1
      scroll-conservatively 10000)
#+END_SRC
** Temporary 'zoom' into a pane
A bit like 'focus mode' for other languages.  Can perhaps combine this with /darkroom/ to enforce distraction free writing.

From [[https://github.com/kaushalmodi/.emacs.d/blob/master/setup-files/setup-windows-buffers.el][this github repo]].
#+BEGIN_SRC emacs-lisp
(defvar modi/toggle-one-window--buffer-name nil
  "Variable to store the name of the buffer for which the `modi/toggle-one-window'
function is called.")

(defvar modi/toggle-one-window--window-configuration nil
  "Variable to store the window configuration before `modi/toggle-one-window'
function was called.")

(defun modi/toggle-one-window (&optional force-one-window)
  "Toggles the frame state between deleting all windows other than
the current window and the windows state prior to that."
  (interactive "P")
  (if (or (null (one-window-p))
          force-one-window)
      (progn
        (setq modi/toggle-one-window--buffer-name (buffer-name))
        (setq modi/toggle-one-window--window-configuration (current-window-configuration))
        (delete-other-windows))
    (progn
      (when modi/toggle-one-window--buffer-name
        (set-window-configuration modi/toggle-one-window--window-configuration)
        (switch-to-buffer modi/toggle-one-window--buffer-name)))))
  (define-key global-map (kbd "C-x 1") 'modi/toggle-one-window)


#+END_SRC
** Code-folding
#+BEGIN_SRC emacs-lisp
;; Code folding
(use-package fold-dwim :ensure t :defer t)
(use-package fold-dwim-org :ensure t :defer t)
(add-hook 'prog-mode-hook 'fold-dwim-org/minor-mode)
#+END_SRC
** OTHER appearance stuff
#+BEGIN_SRC emacs-lisp
(setq uniquify-buffer-name-style 'forward)
(setq linum-format "%4d ")

(set-default 'indicate-empty-lines t)
(highlight-indentation-mode -1)

;; Soft-wrap at a column.
;; Not currently setting this globally, as still unsure about 
;; hard wrap vs soft-wrap vs soft-wrap with ruler
(use-package visual-fill-column :ensure t
  :config
  (setq visual-fill-column-width 80))

;; Replace lambda and AND etc with pretty symbols
(global-prettify-symbols-mode +1)
#+END_SRC
* WIP Language Server Protocol 
Not enabling this yet, as I'm not sure how well it incorporates across platforms and with my current languages.

LSP vs elpy?
#+BEGIN_SRC emacs-lisp :tangle no
(use-package lsp-mode :ensure t)
#+END_SRC

