#+TITLE: My Emacs configuration

#+AUTHOR: Chris Davison
#+EMAIL: c.jr.davison@gmail.com
#+OPTIONS: toc:nil
#+PROPERTY: header-args  :results silent
#+REVEAL_ROOT: http://cdn.jsdelivr.net/reveal.js/2.5.0/
#+REVEAL_THEME: moon 
# solarized

* about this file
Inspired by [[http://pages.sachachua.com/.emacs.d/Sacha.html#unnumbered-3][Sacha Chua's Config]], I decided to move from a multi-file approach, to use org mode's power to really easily manage the complexity.

I can selectively export functionality by choosing to *tangle* the source code or not.

By adding the following into your emacs =init.el=, you can then move all your config into a documented org file.

#+BEGIN_SRC emacs-lisp  :tangle no
  ;; This sets up the load path so that we can override it
  (package-initialize nil)

  ;; Load the rest of the packages
  (package-initialize t)
  (setq package-enable-at-startup nil)

  (add-to-list 'package-archives '("org" . "http://orgmode.org/elpa/"))
  (add-to-list 'package-archives '("melpa" . "http://melpa.org/packages/"))

  ;; Try the 'use package' macro, from jwiegley on github
  (unless (package-installed-p 'use-package)
    (package-refresh-contents)
    (package-install 'use-package))

  ;; From here on, the org config will use the use-package macro for installing and configuring packages
  (require 'use-package)

  ;; Finally, load my literate config
  (org-babel-load-file "~/.emacs.d/Chris.org")
#+end_src

* initialisation
** consts
Basically give a useful const to check if running OSX.  This may be useful for your config, to override platform-specific behaviour (or perhaps implement some more specificity).
#+BEGIN_SRC emacs-lisp
  (defconst *spell-check-support-enabled* nil)
  (defconst *is-a-mac* (eq system-type 'darwin))
#+END_SRC

** package management
Refresh the package list, to get newest versions.  Can cause a slight delay when opening emacs.
Add org and use melpa for most packages.

#+BEGIN_SRC emacs-lisp :tangle no
  ;; Tangle to update packages, or call yourself
  (package-refresh-contents)
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (add-to-list 'package-archives '("org" . "http://orgmode.org/elpa/"))
  (add-to-list 'package-archives '("melpa" . "http://melpa.org/packages/"))
#+END_SRC

** eval after load
#+BEGIN_SRC emacs-lisp
(if (fboundp 'with-eval-after-load)
  (defalias 'after-load 'with-eval-after-load)
  (defmacro after-load (feature &rest body)
    "After FEATURE is loaded, evaluate BODY."
    (declare (indent defun))
    `(eval-after-load ,feature
                      '(progn ,@body))))
#+END_SRC

** file deletion utilities
Delete the current file.  Basically a big of sugar around =delete-file= to take the current buffer.
#+BEGIN_SRC emacs-lisp
(defun delete-this-file ()
  "Delete the current file, and kill the buffer."
  (interactive)
  (or (buffer-file-name) (error "No file is currently being edited"))
  (when (yes-or-no-p (format "Really delete '%s'?"
                             (file-name-nondirectory buffer-file-name)))
    (delete-file (buffer-file-name))
    (kill-this-buffer)))
#+END_SRC

** easier add-to-list
   Give regex patterns to add-auto-mode, so you don't need to call add-to-list multiple times (it'll do it for you).
   #+BEGIN_SRC emacs-lisp
(defun add-auto-mode (mode &rest patterns)
  "Add entries to `auto-mode-alist' to use `MODE' for all given file `PATTERNS'."
  (dolist (pattern patterns)
    (add-to-list 'auto-mode-alist (cons pattern mode))))
   #+END_SRC
   
** and then initialize...
#+BEGIN_SRC emacs-lisp
(setq package-enable-at-startup nil)
(package-initialize)

(use-package fullframe
  :ensure t
  :config (fullframe list-packages quit-window))
#+END_SRC

** common lisp libs
Useful to have access to power of common lisp within emacs.
#+BEGIN_SRC emacs-lisp
(use-package cl-lib
  :ensure t
  :config (require 'cl-lib))
#+END_SRC

** diminish minor modes
#+BEGIN_SRC emacs-lisp
(use-package diminish :ensure t)
#+END_SRC

* appearance
** font stuff
   #+BEGIN_SRC emacs-lisp
(when (member "DejaVu Sans Mono" (font-family-list))
  (set-face-attribute 'default nil :font "DejaVu Sans Mono" :height 140))
(setq line-spacing 0.2)
   #+END_SRC
   
** colour theme 
   I always end up going back to solarized, but have been using 'github' within VIM, so should look into alternatives.  Also added functions to quickly jump between light and dark themes.
   #+BEGIN_SRC emacs-lisp
     (use-package solarized-theme :ensure t)
     (use-package zenburn-theme :ensure t)
     (use-package molokai-theme :ensure t)

     (defun light()
       "Activate a light color theme."
       (interactive)
       (load-theme 'solarized-light))

     (defun dark()
       "Activate a dark color theme."
       (interactive)
       (load-theme 'solarized-dark))

     (load-theme 'solarized-dark t)
   #+END_SRC
   
** *maybe* suspend frame
   This is useful if running a mac, so that C-z wont hide it.  Considered 'appearance' as I want to see windows.
   #+BEGIN_SRC emacs-lisp
(defun maybe-suspend-frame ()
  (interactive)
  (unless (and *is-a-mac* window-system)
    (suspend-frame)))

(global-set-key (kbd "C-z") 'maybe-suspend-frame)
   #+END_SRC
   
** suppress gui features
   #+BEGIN_SRC emacs-lisp
  (setq use-file-dialog nil)
  (setq use-dialog-box nil)
  (setq inhibit-startup-screen t)
  (setq inhibit-startup-echo-area-message t)
   #+END_SRC
   
   Hide tool bar,  scroll bar and borders
   #+BEGIN_SRC emacs-lisp
(when (fboundp 'tool-bar-mode)
  (tool-bar-mode -1))
(when (fboundp 'set-scroll-bar-mode)
  (set-scroll-bar-mode nil))

(let ((no-border '(internal-border-width . 0)))
  (add-to-list 'default-frame-alist no-border)
  (add-to-list 'initial-frame-alist no-border))
   #+END_SRC
   
** highlight current line and no blinking cursor
   #+BEGIN_SRC emacs-lisp
(global-hl-line-mode 1)
(blink-cursor-mode 0)
(setq linum-format "%d ")
   #+END_SRC
   
** alias for yes-or-no
   This is again a quality of life thing, to allow you to hit yes or no by using y or n.
   #+BEGIN_SRC emacs-lisp
(defalias 'yes-or-no-p 'y-or-n-p)
   #+END_SRC
   
** faster keystroke echoing
   #+BEGIN_SRC emacs-lisp
(setq echo-keystrokes 0.1)
   #+END_SRC
   
** tabs to spaces, and tab-related things
   #+BEGIN_SRC emacs-lisp
(setq tab-stop-list (number-sequence 2 200 2))
(setq-default indent-tabs-mode nil)
(setq-default tab-width 2)
   #+END_SRC
   
** show line and column number in status bar
   #+BEGIN_SRC emacs-lisp
(line-number-mode 1)
(column-number-mode 1)
   #+END_SRC
   
** stop scrolling jumping multiple lines
   #+BEGIN_SRC emacs-lisp
  (setq scroll-step           1
        scroll-conservatively 10000)
   #+END_SRC
   
** scratch buffer - initial message
   #+BEGIN_SRC emacs-lisp
  (setq-default initial-scratch-message ";; Scratch pad\n\n")
   #+END_SRC
   
** font-locking
   Replace any occurrence of 'lambda' with the actual symbol.
   #+BEGIN_SRC emacs-lisp 
  (global-prettify-symbols-mode +1)
   #+END_SRC
   
* parentheses
Show matching parens.  Use paredit to make shifting parens easier, and colourise parens using rainbow delimeters to increase visual clarity.
#+BEGIN_SRC emacs-lisp
  (use-package paredit
    :ensure t
    :init (show-paren-mode t))

  (use-package rainbow-delimiters
    :ensure t
    :config (add-hook 'prog-mode-hook 'rainbow-delimiters-mode))
#+END_SRC

* vim
Being a VIM user...Emacs' keybindings are quite nasty.  As such, I try to make this editing experience as close to the VIM experience as possible, while allowing for the nicety of Emacs.  Key-chord is pretty nice to keep my key presses down.

#+BEGIN_SRC emacs-lisp
    (use-package evil
    :ensure t
    :config (evil-mode 1))

  (use-package evil-surround
    :ensure t
    :config (global-evil-surround-mode))


  (use-package evil-leader 
    :ensure t 
    :config (global-evil-leader-mode))

  (use-package key-chord
    :ensure t
    :config (key-chord-mode 1))

  (define-key evil-normal-state-map (kbd "C-h") 'evil-window-left)
  (define-key evil-normal-state-map (kbd "C-j") 'evil-window-down)
  (define-key evil-normal-state-map (kbd "C-k") 'evil-window-up)
  (define-key evil-normal-state-map (kbd "C-l") 'evil-window-right)

  (define-key evil-normal-state-map (kbd ";") 'evil-ex)

  (key-chord-define-global "jk" 'evil-normal-state)
  (key-chord-define-global "gc" 'comment-or-uncomment-region)

  ;; Some of these functions are only pulled in later
  ;; But VIM is added early incase customisation breaks.
  (evil-leader/set-key
    "w" 'save-buffer
    "f" 'helm-find-files
    "h" 'helm-mini
    "o" 'helm-occur
    "s" 'helm-swoop
    "i" 'helm-imenu
    "j" 'jump-to-register
    "k" 'kill-buffer)

#+END_SRC

* history, backups and session
Disk space is plentiful.  Keep backups and history.  Also, move the backups to the appropriate dir, so the backup files =.*~= don't clutter.

#+begin_src emacs-lisp
  (setq backup-directory-alist '(("." . "~/.emacs.d/backups")))
  (setq delete-old-versions -1)
  (setq version-control t)
  (setq vc-make-backup-files t)
  (setq auto-save-file-name-transforms '((".*" "~/.emacs.d/auto-save-list/" t)))


  (setq savehist-file "~/.emacs.d/savehist")
  (savehist-mode 1)
  (setq history-length t)
  (setq history-delete-duplicates t)
  (setq savehist-save-minibuffer-history 1)
  (setq savehist-additional-variables
        '(kill-ring
          search-ring
          regexp-search-ring))

  ;; Counting 'recent files' as part of history
  (use-package recentf
    :ensure t
    :config (recentf-mode))
  (setq recentf-max-saved-items 200)
  (setq recentf-max-menu-items 15)
  (recentf-mode)

  ;; Always save the desktop, the current workspace config
  (setq desktop-path (list user-emacs-directory)
        desktop-auto-save-timeout 600)
  (desktop-save-mode 1)
#+end_src

** recent files

#+begin_src emacs-lisp
(require 'recentf)
(setq recentf-max-saved-items 200
      recentf-max-menu-items 15)
(recentf-mode)
#+end_src

* undo tree  - visualize your undos and branches

People often struggle with the Emacs undo model, where there's really no concept of "redo" - you simply undo the undo.

This lets you use =C-x u= (=undo-tree-visualize=) to visually walk through the changes you've made, undo back to a certain point (or redo), and go down different branches.

#+begin_src emacs-lisp
  (use-package undo-tree
    :diminish undo-tree-mode
    :config 
    (global-undo-tree-mode)
    (setq undo-tree-visualizer-timestamps nil)
    (setq undo-tree-visualizer-diff t))
#+end_src

* search && navigation
From http://www.masteringemacs.org/articles/2011/03/25/working-multiple-files-dired/

Generally improve dired appearance
#+begin_src emacs-lisp 

  (use-package dired+
    :config (setq find-ls-option '("-print0 | xargs -0 ls -ld" . "-ld")))

  (use-package find-dired
    :ensure dired+
    :config
    (setq find-ls-option '("-print0 | xargs -0 ls -ld" . "-ld"))
    (setq dired-omit-files "^\\.[^.]\\|\\.pdf$\\|\\.tex$") 
    (diredp-toggle-find-file-reuse-dir 1)) 
#+END_SRC

Hide files beginning with .[not a dot]
#+BEGIN_SRC emacs-lisp
  (setq dired-omit-mode t)
  (setq dired-omit-files "^\\.?#\\|^\\.$\\|^\\.\\.$\\|^\\.\\|^__.*$")
#+END_SRC

* searching
show current/total matches when searching
#+BEGIN_SRC emacs-lisp
  (use-package anzu
    :ensure t
    :diminish anzu-mode
    :bind (([remap query-replace-regexp] . anzu-query-replace-regexp)
           ([remap query-replace] . anzu-query-replace))
    :config (global-anzu-mode t))

  ;; DEL during isearch should edit the search string, not jump back to the previous result
  (define-key isearch-mode-map [remap isearch-delete-char] 'isearch-del-char)

  ;; ace-mode is fantastic.  It's a hybrid of ace-jump and isearch
  ;; Hybrid of isearch and ace-jump.  Type a single character in search and words beginning with that will highlight.  Press the highlighted letter to jump to that occurence
  (use-package ace-isearch
    :ensure t
    :config (global-ace-isearch-mode 1))

  ;; smartscan (Vim *)
  ;; From https://github.com/itsjeyd/emacs-config/blob/emacs24/init.el
  ;; This basically allows you to do something similar to VIM *...i.e. it'll jump forward or backward to the next occurence of the symbol under the cursor.
  ;; Bound to =M-n= and =M-b= by default, I think.
  (use-package smartscan
    :ensure t
    :config (global-smartscan-mode t))
#+end_src

* utility
** help - guide-key

It's hard to remember keyboard shortcuts. The =guide-key= package pops up help after a short delay.

#+begin_src emacs-lisp
  (use-package guide-key
    :ensure t
    :init (setq guide-key/guide-key-sequence '("C-x r" "C-x 4" "C-c"))
    :config (guide-key-mode 1))
#+end_src

** utf-8

From http://www.wisdomandwonder.com/wordpress/wp-content/uploads/2014/03/C3F.html
#+begin_src emacs-lisp
(prefer-coding-system 'utf-8)
(when (display-graphic-p)
  (setq x-select-request-type '(UTF8_STRING COMPOUND_TEXT TEXT STRING)))
#+end_src

** mac osx keys
Mac needs a little bit of hand holding...
#+BEGIN_SRC emacs-lisp
  (when *is-a-mac*
    (setq mac-command-modifier 'meta)
    (setq mac-option-modifier 'none)
    (setq default-input-method "MacOSX")
#+END_SRC

Make the mouse wheel/trackpad less jerky
#+BEGIN_SRC emacs-lisp
    ;; Make mouse wheel / trackpad scrolling less jerky
    (setq mouse-wheel-scroll-amount '(1
                                      ((shift) . 5)
                                      ((control))))
    (dolist (multiple '("" "double-" "triple-"))
      (dolist (direction '("right" "left"))
        (global-set-key (kbd (concat "<" multiple "wheel-" direction ">")) 'ignore)))
#+END_SRC

And give emacs some of the expected OS X keybinds
#+BEGIN_SRC emacs-lisp
    (global-set-key (kbd "M-`") 'ns-next-frame)
    (global-set-key (kbd "M-h") 'ns-do-hide-emacs)
    (global-set-key (kbd "M-˙") 'ns-do-hide-others)
    (after-load 'nxml-mode
      (define-key nxml-mode-map (kbd "M-h") nil))
    (global-set-key (kbd "M-ˍ") 'ns-do-hide-others) ;; what describe-key reports for cmd-option-h
    (global-set-key (kbd "M-<up>") 'toggle-frame-fullscreen) ;;Bind Meta-<UP> to fullscreen toggling
    (global-set-key (kbd "<f10>") 'toggle-frame-fullscreen) ;;Bind Meta-<UP> to fullscreen toggling
)
#+END_SRC
** indent after newline
#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "RET") 'newline-and-indent)
  (defun sanityinc/newline-at-end-of-line ()
    "Move to end of line, enter a newline, and reindent."
    (interactive)
    (move-end-of-line 1)
    (newline-and-indent))
#+END_SRC

* dired / nav
** windows - managing window splits
A useful package to prompt with a HUD, letting you to jump to a specific buffer if you have more than 2 open.

#+BEGIN_SRC emacs-lisp
  (use-package switch-window
    :ensure t
    :config (setq switch-window-shortcut-style 'alphabet)
    :bind ("C-x o" . switch-window))
#+END_SRC

** ibuffer - interactive buffer management
Interactively modify buffer list (jump to another buffer, see buffer modes, see full path, delete/kill buffer etc.)
#+BEGIN_SRC emacs-lisp
  (use-package fullframe :ensure t)
  (after-load 'buffer
    (fullframe ibuffer ibuffer-quit))

  (use-package buffer-vc)

  (defun ibuffer-set-up-preferred-filters ()
    (ibuffer-vc-set-filter-groups-by-vc-root)
    (unless (eq ibuffer-sorting-mode 'filename/process)
      (ibuffer-do-sort-by-filename/process)))

  (add-hook 'ibuffer-hook 'ibuffer-set-up-preferred-filters)

  (after-load 'ibuffer
    ;; Use human readable Size column instead of original one
    (define-ibuffer-column size-h
      (:name "Size" :inline t)
      (cond
       ((> (buffer-size) 1000000) (format "%7.1fM" (/ (buffer-size) 1000000.0)))
       ((> (buffer-size) 1000) (format "%7.1fk" (/ (buffer-size) 1000.0)))
       (t (format "%8d" (buffer-size))))))


  ;; Explicitly require ibuffer-vc to get its column definitions, which
  ;; can't be autoloaded
  (after-load 'ibuffer
    (require 'ibuffer-vc))

  ;; Modify the default ibuffer-formats (toggle with `)
  (setq ibuffer-formats
        '((mark modified read-only vc-status-mini " "
                (name 18 18 :left :elide) " "
                (size-h 9 -1 :right) " "
                (mode 16 16 :left :elide) " "
                filename-and-process)
          (mark modified read-only vc-status-mini " "
                (name 18 18 :left :elide) " "
                (size-h 9 -1 :right) " "
                (mode 16 16 :left :elide) " "
                (vc-status 16 16 :left) " "
                filename-and-process)))

  (setq ibuffer-filter-group-name-face 'font-lock-doc-face)

  (global-set-key (kbd "C-x C-b") 'ibuffer)

#+END_SRC

* word wrapping and truncation
Couldn't get this working directly...so functionalise it
#+BEGIN_SRC emacs-lisp
  (defun trunc-wrap()
    "Turn on truncation and word wrapping"
    (interactive)
    (if truncate-lines 
        (progn
          (setq truncate-lines nil)
          (setq word-wrap nil)
          (message "Truncation and word wrap enabled"))
      (progn 
        (setq truncate-lines t)
        (setq word-wrap t)
        (message "Truncation and word wrap disabled"))))

  (key-chord-define-global "tw" 'trunc-wrap)
  (setq truncate-lines t)
  (setq word-wrap t)
#+END_SRC

* prog languages
** quick configs
  Re-writing this to only use use-package, as it clears it up fairly well
#+BEGIN_SRC emacs-lisp
  (use-package flycheck)

  ;; not sure if add-auto-mode works like the setq...so leaving for now
  (use-package markdown-mode
    :config (add-auto-mode 'markdown-mode "\\.\\(md\\|markdown\\)\\'"))

  (use-package csv-mode
    :ensure csv-nav
    :mode ("\\.[Cc][Ss][Vv]\\'" . csv-mode)
    :config (setq csv-separators '("," ";" "|" " ")))

  ;; Emmet is fantastic for quickly outlining HTML
  (use-package emmet-mode
    :config 
    (add-hook 'sgml-mode-hook 'emmet-mode)
    (add-hook 'css-mode-hook 'emmet-mode)
    (setq emmet-move-cursor-between-quotes t))

  (use-package haml-mode)
  (use-package sass-mode)
  (use-package scss-mode
    :config (setq-default scss-compile-at-save t))

  ;; Colourize CSS literals
  (use-package rainbow-mode
    :config 
    (add-hook 'css-mode-hook 'rainbow-mode)
    (add-hook 'html-mode-hook 'rainbow-mode)
    (add-hook 'sass-mode-hook 'rainbow-mode))

  ;; Racket
  (use-package racket-mode
    :config 
    (add-hook 'racket-mode-hook 'paredit-mode)
    (setq racket-racket-program "/usr/local/bin/racket")
    (setq racket-raco-program "/usr/local/bin/raco") 
    (setq racket-program "/usr/local/bin/racket")
    (setq raco-program "/usr/local/bin/raco"))

  ;; Go
  (use-package go-mode
    :config (add-hook 'before-save-hook 'gofmt-before-save))

  ;; Rust
  (use-package rust-mode)
  (use-package flymake-rust)
  (use-package flycheck-rust)

  ;; Python
  (use-package virtualenvwrapper
    :ensure t
    :config
    (venv-initialize-interactive-shells) 
    (venv-initialize-eshell)
    (setq venv-location "/Users/davison/Envs/")
    (add-hook 'python-mode-hook (lambda () (venv-workon "numeric"))))

  (use-package ein :ensure t)
   #+END_SRC
** lisp
   Paredit was included earlier as it's just generally useful.
   
   Give a default message when opening a scratch (elisp) buffer.
   #+BEGIN_SRC emacs-lisp
  (setq-default initial-scratch-message
                (concat ";; Happy hacking, " (or user-login-name "") "!\n\n"))
   #+END_SRC
   
   Automatically byte-compile
   #+begin_src emacs-lisp
     (use-package auto-compile
       :config 
       (auto-compile-on-save-mode 1)
       (auto-compile-on-load-mode 1)
       (setq load-prefer-newer t))

     #+end_src
   
   Hook some useful settings into lisp modes
   #+begin_src emacs-lisp
     (defun davison/useful-lisp ()
       (rainbow-delimiters-mode t)
       (enable-paredit-mode)
       (when (fboundp 'aggressive-indent-mode)
         (aggressive-indent-mode)))

     (add-hook 'lisp-mode 'davison/useful-lisp)
     (add-hook 'emacs-lisp-mode 'davison/useful-lisp)
   #+end_src
   
* ido
Could Helm be a better all-round replacement for Ido?  Don't include IDO for now.
#+BEGIN_SRC emacs-lisp :tangle no
  (use-package ido
    :ensure t
    :config 
    (ido-mode t)
    (ido-everywhere t)
    (setq ido-enable-flex-matching t)
    (setq ido-use-filename-at-point nil)
    (setq ido-auto-merge-work-directories-length 0)
    (setq ido-use-virtual-buffers t))

  (use-package ido-menu
    :ensure ido
    :config 
    (setq ido-default-buffer-method 'selected-window)
    (ido-mode -1))

  (use-package ido-ubiquitous
    :ensure t
    :config (ido-ubiquitous-mode t))

  ;; Use smex to handle M-x
  (use-package smex
    :config
    (setq smex-save-file (expand-file-name ".smex-items" user-emacs-directory))
    (global-set-key [remap execute-extended-command] 'smex))
#+END_SRC

* org-mode
** taking notes
Set up org for taking notes, using Dropbox/docs as my main folder.  Also, set up some nice config for org todo and agenda stuff.

=org-refile= lets you organize notes by typing in the headline to file them under.
#+BEGIN_SRC emacs-lisp
  (defun my/org-setup ()
    (setq org-directory "~/Dropbox/docs")
    (setq org-default-notes-file "~/Dropbox/docs/notes.org")
    (setq org-src-window-setup 'current-window) ; Don't go to a new window when C-c ' editing babel
    (setq org-src-fontify-natively t)
    (setq org-agenda-files (delq nil (mapcar (lambda (x) (and (file-exists-p x) x))
                                             '("~/Dropbox/docs/"))))
    (setq org-todo-keywords '((sequence "-TODO-(t)" "-WIP(w)-" "|" "-DONE(d)-" "-CANCELLED-(c)")))
    ;; Settings for refiling
    (setq org-reverse-note-order t)
    (setq org-refile-use-outline-path nil)
    (setq org-refile-allow-creating-parent-nodes 'confirm)
    (setq org-refile-use-cache nil)
    (setq org-refile-targets '((org-agenda-files . (:maxlevel . 6))))
    (setq org-blank-before-new-entry nil))

  (use-package org
    :ensure t
    :bind (("<f1>" . org-capture)
           ("<f2>" . org-agenda)
           ("<f3>" . org-agenda-list)
           ("<f4>" . org-timeline))
    :config (my/org-setup))



  ;; (global-set-key (kbd "<f1>") 'org-capture)
  ;; (global-set-key (kbd "<f2>") 'org-agenda)
  ;; (global-set-key (kbd "<f3>") 'org-agenda-list)
  ;; (global-set-key (kbd "<f4>") 'org-timeline)

  (use-package ox-reveal :ensure t)
  (use-package htmlize :ensure t)
#+END_SRC

This makes it easier to add links from outside.
#+begin_src emacs-lisp
(defun sacha/yank-more ()
  (interactive)
  (insert "[[")
  (yank)
  (insert "][more]]"))
(global-set-key (kbd "<f6>") 'sacha/yank-more)
#+end_src

** capture - templates
   =org-capture= lets you create templates for jotting down info of various kinds.

#+BEGIN_SRC emacs-lisp
    (setq org-capture-templates
          '(("i" "ideas" entry
             (file+headline "~/Dropbox/docs/notes.org" "Ideas")
             "** %^{Idea} \n%?"
             :immediate-finish)

            ("q" "quotes" entry
             (file+headline "~/Dropbox/docs/notes.org" "Quotes")
             "** %^{WHO? WHERE?}\n%^{QUOTE}"
             :immediate-finish)

            ("t" "todo" entry
             (file+headline "~/Dropbox/docs/notes.org" "Tasks")
             "** TODO %^{Task}\n%^{WHEN?}t\n%^{DETAILS}"
             :immediate-finish)

            ("e" "engd" entry
             (file+headline "~/Dropbox/docs/notes.org" "EngD")
             "** %^{WHAT}\n%?"
             :immediate-finish)))
#+end_src

** keyboard shortcuts
#+begin_src emacs-lisp :tangle no
    (global-set-key (kbd "<f1>") 'org-capture)
    (global-set-key (kbd "<f2>") 'org-agenda)
    (global-set-key (kbd "<f3>") 'org-agenda-list)
    (global-set-key (kbd "<f4>") 'org-timeline)
#+end_src

** indent org babel src
In an Org-Babel block, run my/org-cleanup to fix indentation
#+BEGIN_SRC emacs-lisp
  (defun my/org-cleanup ()
    (interactive)
    (org-edit-special)
    (indent-buffer)
    (org-edit-src-exit))

  (defun indent-buffer ()
    (interactive)
    (indent-region (point-min) (point-max)))
#+END_SRC

* Jump to commonly used files (set as registers)
Set files as registers and then comfortably jump to them 
#+BEGIN_SRC emacs-lisp :tangle no
  (global-set-key (kbd "C-x j") 'jump-to-register)
  (set-register ?c (cons 'file "~/.emacs.d/Chris.org"))
  (set-register ?u (cons 'file "~/Dropbox/org/uni.org"))
  (set-register ?n (cons 'file "~/Dropbox/org/notes.org"))
  (set-register ?r (cons 'file "~/Dropbox/org/rust.org"))
  (set-register ?w (cons 'file "~/Dropbox/org/wh40k.org"))
  (set-register ?d (cons 'file "~/Dropbox/org/dungeon-world.org"))
  (set-register ?j (cons 'file "~/Dropbox/org/clojure.org"))
  (set-register ?m (cons 'file "~/Dropbox/org/numenera.org"))
  (set-register ?s (cons 'file "~/Dropbox/org/scrolls.org"))
  (set-register ?t (cons 'file "~/.tmp.org"))
#+END_SRC

* helm
general helm setup
#+BEGIN_SRC emacs-lisp
  (use-package helm
    :ensure t
    :init (require 'helm-config)
    :config 
    (setq helm-candidate-number-limit 100
          helm-idle-delay 0.0
          helm-input-idle-delay 0.01
          helm-quick-update t
          helm-M-x-requires-pattern nil
          helm-ff-skip-boring-files t
          helm-recentf-fuzzy-match t
          helm-buffers-fuzzy-matching t
          helm-locate-fuzzy-match t
          helm-imenu-fuzzy-match t
          helm-M-x-fuzzy-match t
          helm-semantic-fuzzy-match t
          helm-apropos-fuzzy-match t
          helm-split-window-in-side-p t)
    (helm-mode 1)
    (helm-autoresize-mode 1))

  (use-package helm-descbinds :ensure t)
#+END_SRC

key bindings
#+BEGIN_SRC emacs-lisp 
  (global-set-key (kbd "C-c h") 'helm-mini)
  (global-set-key (kbd "C-c a") 'helm-apropos)
  (global-set-key (kbd "C-c o") 'helm-occur)
  (global-set-key (kbd "C-c s") 'helm-swoop)
  (global-set-key (kbd "C-c i") 'helm-imenu)
  (global-set-key (kbd "C-c b") 'helm-descbinds)
  (global-set-key (kbd "C-c r") 'helm-regexp)

  (global-set-key (kbd "C-x C-f") 'helm-find-files)

  (global-set-key (kbd "M-y") 'helm-show-kill-ring)
  (global-set-key (kbd "M-x") 'helm-M-x)
#+END_SRC

#+BEGIN_SRC emacs-lisp
#+END_SRC

* aggressive indentation
#+BEGIN_SRC emacs-lisp
  (use-package aggressive-indent
    :ensure t
    :config (global-aggressive-indent-mode))
#+END_SRC
* latex
** general config
#+BEGIN_SRC emacs-lisp :tangle no
  (require-package 'auctex)
  (setq TeX-auto-save t)
  (setq TeX-parse-self t)
  (setq TeX-save-query nil)

  (setq ispell-program-name "aspell") ; could be ispell as well, depending on your preferences
  (setq ispell-dictionary "english") ; this can obviously be set to any language your spell-checking program supports

  (add-hook 'LaTeX-mode-hook 'flyspell-mode)
  (add-hook 'LaTeX-mode-hook 'flyspell-buffer)

  (defun turn-on-outline-minor-mode ()
  (outline-minor-mode 1))

  (add-hook 'LaTeX-mode-hook 'turn-on-outline-minor-mode)
  (add-hook 'latex-mode-hook 'turn-on-outline-minor-mode)
  (setq outline-minor-mode-prefix "\C-c \C-o") ; Or something else
#+END_SRC

** manage citations
#+BEGIN_SRC :tangle no
  (require 'tex-site)
  (autoload 'reftex-mode "reftex" "RefTeX Minor Mode" t)
  (autoload 'turn-on-reftex "reftex" "RefTeX Minor Mode" nil)
  (autoload 'reftex-citation "reftex-cite" "Make citation" nil)
  (autoload 'reftex-index-phrase-mode "reftex-index" "Phrase Mode" t)
  (add-hook 'latex-mode-hook 'turn-on-reftex)
  (add-hook 'LaTeX-mode-hook 'turn-on-reftex)

  (setq LaTeX-eqnarray-label "eq"
        LaTeX-equation-label "eq"
        LaTeX-figure-label "fig"
        LaTeX-table-label "tab"
        LaTeX-myChapter-label "chap"
        TeX-auto-save t
        TeX-newline-function 'reindent-then-newline-and-indent
        TeX-parse-self t
        TeX-style-path
        '("style/" "auto/"
          "/usr/share/emacs21/site-lisp/auctex/style/"
          "/var/lib/auctex/emacs21/"
          "/usr/local/share/emacs/site-lisp/auctex/style/")
        LaTeX-section-hook
        '(LaTeX-section-heading
          LaTeX-section-title
          LaTeX-section-toc
          LaTeX-section-section
          LaTeX-section-label))
#+END_SRC

   
   
